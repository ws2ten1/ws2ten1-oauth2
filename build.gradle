// ================
// Gradle configuration

plugins {
	id 'java-library'
	id 'maven-publish'
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'com.jfrog.bintray' version '1.8.4'

	// https://github.com/palantir/gradle-git-version
	id 'com.palantir.git-version' version '0.12.2'

	// code quality plugins
	id 'checkstyle'
	id 'pmd'
	id 'jacoco'
	id 'com.github.spotbugs' version '3.0.0' apply false
	id 'de.thetaphi.forbiddenapis' version '2.7'
	id 'de.aaschmid.cpd' version '3.1'
	id 'com.diffplug.gradle.spotless' version '3.27.1'
}

wrapper {
	gradleVersion = '6.0.1'
	distributionType = Wrapper.DistributionType.ALL
}

// ================
// Project metadata configuration

group = 'org.polycreo'
version = gitVersion()

// ================
// Build configuration

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
	options.compilerArgs << '-Werror'
	options.compilerArgs << '-Xlint:all' << '-Xlint:-processing' << '-Xlint:-deprecation'
}

apply plugin: 'io.spring.dependency-management'

repositories {
	jcenter()
	mavenCentral()
	maven { url 'https://dl.bintray.com/polycreo/maven' }
}

dependencies {
	api 'org.slf4j:slf4j-api'
	api 'org.springframework:spring-web'
	api 'org.springframework.security:spring-security-core:5.2.2.RELEASE'
	api 'org.springframework.security:spring-security-config:5.2.2.RELEASE'
	api 'org.springframework.security:spring-security-oauth2-core:5.2.2.RELEASE'
	api 'org.springframework.security:spring-security-oauth2-resource-server:5.2.2.RELEASE'
	implementation 'org.springframework.security:spring-security-oauth2-jose'
	implementation 'com.nimbusds:oauth2-oidc-sdk:9.5.3'
	implementation 'com.fasterxml.jackson.core:jackson-databind'

	compileOnly 'javax.servlet:javax.servlet-api:4.0.1'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}

// ================
// Lint / Test configuration

apply from: "${rootProject.projectDir}/config/checkstyle.gradle"
apply from: "${rootProject.projectDir}/config/spotbugs.gradle"
apply from: "${rootProject.projectDir}/config/pmd.gradle"
apply from: "${rootProject.projectDir}/config/cpd.gradle"
apply from: "${rootProject.projectDir}/config/jacoco.gradle"
apply from: "${rootProject.projectDir}/config/forbiddenapis.gradle"
apply from: "${rootProject.projectDir}/config/spotless.gradle"

tasks.withType(com.github.spotbugs.SpotBugsTask) {
	reports {
		html.enabled = true
		xml.enabled = false
	}
}

tasks.withType(Test) {
	exclude "**/*\$*", "**/*Tests.class", "**/examples/**"
	reports {
		html.enabled = true
		junitXml.outputPerTestCase = false
	}
	testLogging.exceptionFormat = 'full'
	beforeTest {
		logger.lifecycle("{} > {}", it.parent.name, it.name)
	}
}

// ================
// Deploy configuration

apply from: "${rootProject.projectDir}/gradle/sourceArtifact.gradle"
apply from: "${rootProject.projectDir}/gradle/publish.gradle"
